plugins {
    id "com.github.johnrengelman.shadow" version "2.0.0"
}

repositories {
    jcenter()
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'distribution'
apply plugin: 'com.github.johnrengelman.shadow'

project.ext {

    main_class = 'tw.howie.spark.SparkApplication'
    jar_name = "spark-codegen-error-" + version + "-all.jar"
    local_jar_path = "build/libs/" + jar_name


}

compileJava {
    sourceCompatibility = java_version
    targetCompatibility = java_version

    // Show all warnings except boot classpath
    configure(options) {
        encoding = "utf-8"
        incremental = true                      //enable incremental compilation
        fork = true                             //enable compilation in a separate daemon process
    }

}

compileTestJava {
    sourceCompatibility = java_version
    targetCompatibility = java_version
}


clean {

    delete "out"    //Intellij output
    delete "target" //Eclipse
    delete "build"  //Gradle
    delete "bin"

}

test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true
}


shadowJar {
    relocate 'com.google', 'shadow.com.google'
    relocate 'org.apache.commons.cli', 'shadow.org.apache.commons.cli'
    manifest {
        attributes 'Main-Class': main_class
    }
    zip64 true
}

distZip {
    archiveName = "${project.name}.zip"
    zip64 true
}

distributions {
    main {
        contents {
            from('src/main/scripts') {
                into 'bin'
            }

            from(shadowJar.archivePath) {
                into 'libs'
            }
        }
    }
}

dependencies {


    compile 'commons-cli:commons-cli:1.3.1'
    compile 'com.google.guava:guava:15.0'
    compile 'org.slf4j:slf4j-api:1.7.16'
    compile 'org.slf4j:slf4j-log4j12:1.7.16'

    // Apache Spark
    compileOnly 'org.apache.spark:spark-mllib_2.11:2.3.0'
    compileOnly('org.apache.spark:spark-sql_2.11:2.3.0') {
        exclude module: 'commons-cli'
        exclude module: 'guava'
    }
    compileOnly 'org.apache.hadoop:hadoop-mapreduce-client-core:2.7.2'
    compileOnly 'org.apache.hadoop:hadoop-common:2.7.2'



    testCompile 'org.apache.spark:spark-sql_2.11:2.3.0'
    testCompile 'org.apache.spark:spark-mllib_2.11:2.3.0'
    // Without this, you will get SLF4J-related error with more recent versions of Spark
    testCompile 'junit:junit:4.+'
    testCompile 'org.assertj:assertj-core-java8:1.0.0m1'


}






